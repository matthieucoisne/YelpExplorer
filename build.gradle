// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://maven.fabric.io/public" }
    }

    dependencies {
        classpath GradlePlugins.kotlin
        classpath GradlePlugins.fabric
        classpath GradlePlugins.android
        classpath GradlePlugins.androidx_navigation_safe_args
        classpath GradlePlugins.google_services
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.26.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://maven.fabric.io/public" }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                buildToolsVersion Config.buildTools
                compileSdkVersion Config.compileSdk

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                flavorDimensions "server"
                productFlavors {
                    dev
                }

                variantFilter { variant ->
                    def buildType = variant.buildType.name
                    def flavor = variant.flavors.get(0).name
                    if (buildType == "release") {
                        if (flavor == "dev") {
                            variant.setIgnore(true)
                        }
                    }
                }

                compileOptions {
                    sourceCompatibility Versions.java
                    targetCompatibility Versions.java
                }

                viewBinding {
                    enabled = true
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
